plugins {
    id "org.springframework.boot" version "2.5.0-SNAPSHOT"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "net.ltgt.apt" version "0.21"
    id "application"
    id "jacoco"
    id "java"
}

java {
    withJavadocJar()
    withSourcesJar()
}

ext {
    set("mapstructVersion", "1.4.2.Final")
    set("lombokMapstructBinding", "0.2.0")
    set("commonsCollections", "4.3")
    set("swaggerVersion", "2.9.2")
    set("openApiVersion", "1.5.3")
    set("hamcrestVersion", "1.3")
}

group = "com.ipaixao.ibeer"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

applicationName = "i-beer"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springdoc:springdoc-openapi-ui:${openApiVersion}"

//    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
//    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.apache.commons:commons-collections4:${commonsCollections}"
    implementation "org.postgresql:postgresql"
    implementation "org.liquibase:liquibase-core"
    implementation "org.yaml:snakeyaml"

    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBinding}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    compileOnly "org.projectlombok:lombok"

    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "org.springframework.security:spring-security-test"
    testRuntime "com.h2database:h2"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}
tasks.withType(Javadoc) {
    options.addStringOption("source", "15")
    options.addBooleanOption("-enable-preview", true)
}

bootBuildImage {
    environment = [
            "BP_JVM_VERSION": "15.*"
    ]
    imageName = "${project.group}/${applicationName}:${project.version}"
}

application {
    mainClassName = "com.ipaixao.ibeer.IBeerApplication"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
    }
}
